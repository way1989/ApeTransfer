apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ape.transfer"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode VERSION_CODE.toInteger()
        versionName VERSION_NAME
        buildConfigField "String", "BUGLY_APPID", BUGLY_APPID
        buildConfigField "int", "FEEDBACK_APPKEY", FEEDBACK_APPKEY
    }
    signingConfigs {
        release {
            def filePrivateProperties = file("../private.properties")
            if (filePrivateProperties.exists()) {
                Properties propsPrivate = new Properties()
                propsPrivate.load(new FileInputStream(filePrivateProperties))

                storeFile file(propsPrivate['key.store'])
                keyAlias propsPrivate['key.alias']
                storePassword propsPrivate['key.store.password']
                keyPassword propsPrivate['key.alias.password']
            }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->//获取 variant 参数，就是 productFlavor x buildType
        variant.outputs.all {
            if (outputFileName.endsWith('-release.apk')) {//变种名以release.apk结尾时才修改名字
                outputFileName = outputFileName.replace(outputFileName, "ApeTransfer" + "_" + variant.versionName + ".apk")
            } else if (outputFileName.endsWith('-debug.apk')) {
                outputFileName = outputFileName.replace(outputFileName, "ApeTransfer-debug" + "_" + variant.versionName + ".apk")
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation "junit:junit:$rootProject.junitVersion"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"
    //bugly
    implementation "com.tencent.bugly:crashreport:$rootProject.buglyVersion"
    implementation "org.nanohttpd:nanohttpd:$rootProject.nanohttpdVersion"
    implementation "com.google.zxing:core:$rootProject.zxingVersion"

    //rxjava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    //rxandroid
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"
    //rxpermissions
    implementation "com.github.tbruyelle:rxpermissions:$rootProject.rxpermissionsVersion"
    //rxbinding
    implementation "com.jakewharton.rxbinding3:rxbinding:$rootProject.rxbindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rootProject.rxbindingVersion"

    implementation 'com.cantrowitz:rxbroadcast:2.0.0'
    //RecyclerView Adapter Helper
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:$rootProject.adapterHelperVersion"

    implementation 'com.daimajia.numberprogressbar:library:1.2@aar'//数字进度条
    //    compile project(':emoji')
    //    compile project(':filepicker')
    //    compile project(':photopicker')
    implementation project(':backuprestore')
    //google Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    implementation 'com.ufreedom.uikit:FloatingViewLib:1.0.1'
    implementation 'com.mikepenz:actionitembadge:3.3.0@aar'
    //SUB-DEPENDENCIES
    //Android-Iconics - used to provide an easy API for icons
    implementation 'com.mikepenz:iconics-core:2.8.0@aar'

    //loading layout
    implementation 'com.lai.weavey:loadinglayout:1.2'
}
